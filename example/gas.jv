pipeline GasReservePipeline {
	block GasReserveHttpExtractor oftype HttpExtractor {
		url: "https://www.bundesnetzagentur.de/_tools/SVG/js2/_functions/csv_export.html?view=renderCSV&id=1089590";
	}

	pipe {
    	from: GasReserveHttpExtractor;
    	to: GasReserveCSVInterpreter;
    }

	block GasReserveCSVInterpreter oftype CSVInterpreter {
 		delimiter: ";";
	}

	pipe {
    	from: GasReserveCSVInterpreter;
    	to: DateHeaderWriter;
    }

	block DateHeaderWriter oftype CellWriter {
    	at: cell A1;
    	write: "Datum";
    }

    pipe {
       	from: DateHeaderWriter;
      	to: GasReserveTableInterpreter;
    }

	block GasReserveTableInterpreter oftype TableInterpreter {
    	header: true;
    	columns: [
    		"Datum" typed text,
    		"Kritisch" typed decimal,
    		"Angespannt" typed decimal,
    		"Stabil" typed decimal,
    		"Speicherstand IST" typed decimal,
    		"gesetzliche Ziele" typed decimal
    	];
    }

    pipe {
        from: GasReserveTableInterpreter;
        to: GasReserveLoader;
    }

	block GasReserveLoader oftype PostgresLoader {
		host: requires DB_HOST;
		port: requires DB_PORT;
		username: requires DB_USERNAME;
		password: requires DB_PASSWORD;
		database: requires DB_DATABASE;
		table: requires DB_TABLE;
	}
}
