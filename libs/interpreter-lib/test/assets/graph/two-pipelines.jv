// SPDX-FileCopyrightText: 2025 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

pipeline CarsPipeline {

  CarsExtractor
    -> CarsTextFileInterpreter;

  CarsTextFileInterpreter
    -> CarsCSVInterpreter
    -> NameHeaderWriter
    -> CarsTableInterpreter
    -> CarsLoader;

  block CarsExtractor oftype HttpExtractor {
    url: "https://gist.githubusercontent.com/noamross/e5d3e859aa0c794be10b/raw/b999fb4425b54c63cab088c0ce2c0d6ce961a563/cars.csv";
  }

  block CarsTextFileInterpreter oftype TextFileInterpreter { }

  block CarsCSVInterpreter oftype CSVInterpreter {
    enclosing: '"';
  }

  block NameHeaderWriter oftype CellWriter {
    at: cell A1;

    write: [
      "name"
    ];
  }

  valuetype Car {
    property name oftype text;
    property mpg oftype decimal;
    property cyl oftype integer;
    property disp oftype decimal;
    property hp oftype integer;
    property drat oftype decimal;
    property wt oftype decimal;
    property qsec oftype decimal;
    property vs oftype integer;
    property am oftype integer;
    property gear oftype integer;
    property carb oftype integer;
  }

  transform CarParser {
    from r oftype Collection<text>;
    to car oftype Car;

    car: {
      name: asText (r . "name"),
      mpg: asDecimal (r . "mpg"),
      cyl: asInteger (r . 2),
      disp: asDecimal (r . 3),
      hp: asInteger (r . "hp"),
      drat: asDecimal (r . "drat"),
      wt: asDecimal (r . "wt"),
      qsec: asDecimal (r . "qsec"),
      vs: asInteger (r . "vs"),
      am: asInteger (r . "am"),
      gear: asInteger (r . "gear"),
      carb: asInteger (r . "carb")
    };
  }

  block CarsTableInterpreter oftype TableInterpreter {
    header: true;
    columns: Car;
    parseWith: CarParser;
  }

  block CarsLoader oftype SQLiteLoader {
    table: "Cars";
    file: "./cars.sqlite";
  }
}

pipeline ElectricVehiclesPipeline {
  // See here for meta-data of the data source
  // https://catalog.data.gov/dataset/electric-vehicle-population-data/resource/fa51be35-691f-45d2-9f3e-535877965e69

  // 2. At the top of a pipeline, we describe the
  // structure of the pipeline. The first part until 
  // the ElectricRangeTransformer is a linear sequence
  // of blocks. From there we can see a split into two
  // parallel sequences that load the data in to two
  // different sinks.
  ElectricVehiclesHttpExtractor
    -> ElectricVehiclesTextFileInterpreter
    -> ElectricVehiclesCSVInterpreter
    -> ElectricVehiclesTableInterpreter
    -> ElectricRangeTransformer;

  ElectricRangeTransformer
    -> ElectricVehiclesSQLiteLoader;

  // 3. After the pipeline structure, we define the blocks used.
  block ElectricVehiclesHttpExtractor oftype HttpExtractor {
    url: "https://data.wa.gov/api/views/f6w7-q2d2/rows.csv?accessType=DOWNLOAD";
  }

  block ElectricVehiclesTextFileInterpreter oftype TextFileInterpreter { }

  block ElectricVehiclesCSVInterpreter oftype CSVInterpreter { }

  valuetype ElectricVehicle {
    property vin oftype VehicleIdentificationNumber10;
    property county oftype text;
    property city oftype text;
    property state oftype text;
    property postal oftype text;
    property modelYear oftype integer;
    property make oftype text;
    property model oftype text;
    property evType oftype text;
    property cafvEligibility oftype text;
    property electricRange oftype integer;
    property baseMSRP oftype integer;
    property legislativeDistrict oftype text;
    property dolID oftype integer;
    property location oftype text;
    property utility oftype text;
    property censusTract oftype text;
  }

  transform ElectricVehicleParser {
    from r oftype Collection<text>;
    to ev oftype ElectricVehicle;

    ev: {
      vin: r . "VIN (1-10)",
      county: asText (r . "County"),
      city: asText (r . "City"),
      state: asText (r . "State"),
      postal: asText (r . "Postal Code"),
      modelYear: asInteger (r . "Model Year"),
      make: asText (r . "Make"),
      model: asText (r . "Model"),
      evType: asText (r . "Electric Vehicle Type"),
      cafvEligibility: asText (r . "Clean Alternative Fuel Vehicle (CAFV) Eligibility"),
      electricRange: asInteger (r . "Electric Range"),
      baseMSRP: asInteger (r . "Base MSRP"),
      legislativeDistrict: asText (r . "Legislative District"),
      dolID: asInteger (r . "DOL Vehicle ID"),
      location: asText (r . "Vehicle Location"),
      utility: asText (r . "Electric Utility"),
      censusTract: asText (r . "2020 Census Tract"),
    };
  }


  block ElectricVehiclesTableInterpreter oftype TableInterpreter {
    header: true;
    columns: ElectricVehicle;
    parseWith: ElectricVehicleParser;
  }

  block ElectricRangeTransformer oftype TableTransformer {
    inputColumns: [
      "Electric Range"
    ];
    outputColumn: "Electric Range (km)";
    uses: MilesToKilometers;
  }

  transform MilesToKilometers {
    from miles oftype decimal;
    to kilometers oftype integer;

    kilometers: round (miles * 1.609344);
  }

  block ElectricVehiclesSQLiteLoader oftype SQLiteLoader {
    table: "ElectricVehiclePopulationData";
    file: "./electric-vehicles.sqlite";
  }
}

valuetype VehicleIdentificationNumber10 {
  property attr oftype text;

  constraint capitalized: OnlyCapitalLettersAndDigits on attr;
  constraint len: ExactlyTenCharacters on attr;
}

constraint OnlyCapitalLettersAndDigits on text: value matches /^[A-Z0-9]*$/;

constraint ExactlyTenCharacters on text: lengthof value == 10;
