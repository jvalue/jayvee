// SPDX-FileCopyrightText: 2025 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

pipeline CarsPipeline {

	Extractor
		-> CarsTableInterpreter
		-> CarsTableTransformer
		-> CarsLoader;


	block Extractor oftype CSVExtractor {
		url: "https://gist.githubusercontent.com/noamross/e5d3e859aa0c794be10b/raw/b999fb4425b54c63cab088c0ce2c0d6ce961a563/cars.csv";
		enclosing: '"';
	}

  valuetype CarName {
    property name oftype text;
  }

	block CarsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: CarName;
	}

	transform copy {
		from s oftype text;
		to t oftype text;

		t: s;
	}

	block CarsTableTransformer oftype TableTransformer {
		inputColumns: [
			"name",
		];

		outputColumn: "nameCopy";

		uses: copy;
	}

	block CarsLoader oftype SQLiteLoader {
		table: "Cars";
		file: "./cars.sqlite";
	}
}

composite blocktype CSVExtractor {
	// Properties of the CSVExtractor, some with default values
	property url oftype text;
	property delimiter oftype text: ',';
	property enclosing oftype text: '';
	property enclosingEscape oftype text: '';

	// Input and outputs
	input inputName oftype None;
	output outputName oftype Sheet;

	// Pipeline definition from input, over blocks defined later, to output
	inputName
		->FileExtractor
		->FileTextInterpreter
		->FileCSVInterpreter
		->outputName;

	// Block definitions using values from properties by name
	block FileExtractor oftype HttpExtractor {
		url: url;
	}
	block FileTextInterpreter oftype TextFileInterpreter { }

	block FileCSVInterpreter oftype CSVInterpreter {
		delimiter: delimiter;
		enclosing: enclosing;
		enclosingEscape: enclosingEscape;
	}
}
