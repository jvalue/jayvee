// SPDX-FileCopyrightText: 2025 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

pipeline CarsPipeline {

	CarsExtractor
		-> CarsInterpreter
		-> NameHeaderWriter
		-> CarsTableInterpreter
		-> CarsTableTransformer
		-> CarsLoader;


	block CarsExtractor oftype HttpExtractor {
		url: "https://gist.githubusercontent.com/noamross/e5d3e859aa0c794be10b/raw/b999fb4425b54c63cab088c0ce2c0d6ce961a563/cars.csv";
	}

	block CarsInterpreter oftype CSVFileInterpreter {
		enclosing: '"';
	}

	block NameHeaderWriter oftype CellWriter {
		at: cell A1;

		write: [
			"name"
		];
	}

  valuetype Car {
    property name oftype text;
    property mpg oftype decimal;
    property cyl oftype integer;
    property disp oftype decimal;
    property hp oftype integer;
    property drat oftype decimal;
    property wt oftype decimal;
    property qsec oftype decimal;
    property vs oftype integer;
    property am oftype integer;
    property gear oftype integer;
    property carb oftype integer;
  }

  transform CarParser {
    from r oftype Collection<text>;
    to car oftype Car;

    car: {
      name: asText (r cellInColumn "name"),
      mpg: asDecimal (r cellInColumn "mpg"),
      cyl: asInteger (r cellInColumn 2),
      disp: asDecimal (r cellInColumn 3),
      hp: asInteger (r cellInColumn "hp"),
      drat: asDecimal (r cellInColumn "drat"),
      wt: asDecimal (r cellInColumn "wt"),
      qsec: asDecimal (r cellInColumn "qsec"),
      vs: asInteger (r cellInColumn "vs"),
      am: asInteger (r cellInColumn "am"),
      gear: asInteger (r cellInColumn "gear"),
      carb: asInteger (r cellInColumn "carb")
    };
  }

  block CarsTableInterpreter oftype TableInterpreter {
    header: true;
    columns: Car;
    parseWith: CarParser;
  }

	transform copy {
		from s oftype text;
		to t oftype text;

		t: s;
	}

	block CarsTableTransformer oftype TableTransformer {
		inputColumns: [
			"name",
		];

		outputColumn: "nameCopy";

		uses: copy;
	}

	block CarsLoader oftype SQLiteLoader {
		table: "Cars";
		file: "./cars.sqlite";
	}
}
