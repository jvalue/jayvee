// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype CalendarDates {
  property serviceId oftype text; // Conditional columns are considered required for now
  property date oftype GTFSDate;
  property exceptionType oftype GTFSEnumOneOrTwo; // 1 - Service has been added for the specified date
                                                  // 2 - Service has been removed for the specified date.
}
transform CalendarDatesParser {
  from r oftype SheetRow;
  to dates oftype CalendarDates;

  dates: {
    serviceId: r . "service_id",
    date: r . "date",
    exceptionType: r . "exception_type",
  };
}

/**
* A GTFSCalendarDatesInterpreter interprets a calendar_dates.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#calendar_datestxt
*/
publish composite blocktype GTFSCalendarDatesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> CalendarDatesFilePicker
        -> CalendarDatesTextFileInterpreter
        -> CalendarDatesCSVInterpreter
        -> CalendarDatesTableInterpreter
        -> outputPort;

    block CalendarDatesFilePicker oftype FilePicker {
        path: "/calendar_dates.txt";
    }

    block CalendarDatesTextFileInterpreter oftype TextFileInterpreter { }
    block CalendarDatesCSVInterpreter oftype CSVInterpreter { }

    block CalendarDatesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: CalendarDates;
        parseWith: CalendarDatesParser;
    }
}
