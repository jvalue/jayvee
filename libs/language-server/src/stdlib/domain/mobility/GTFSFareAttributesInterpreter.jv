// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype FareAttributes {
  property fareId oftype text;
  property price oftype GTFSNonNegativeDecimal;
  property currencyType oftype GTFSCurrency;
  property paymentMethod oftype GTFSEnumTwo;  // 0 - Fare is paid on board.
                                              // 1 - Fare must be paid before boarding.
  property transfers oftype text; // Is required but can be empty (?!) so has to be modelled as text...
  property transferDuration oftype text;
}
transform FareAttributesParser {
  from r oftype SheetRow;
  to fareAttributes oftype FareAttributes;

  fareAttributes: {
    fareId: r . 0,
    price: r . 1,
    currencyType: r . 2,
    paymentMethod: r . 3,
    transfers: r . 4,
    transferDuration: r . 5,
  };
}

/**
* A GTFSFareAttributesInterpreter interprets a fare_attributes.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#fare_attributestxt
*/
publish composite blocktype GTFSFareAttributesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> FareAttributesFilePicker
        -> FareAttributesTextFileInterpreter
        -> FareAttributesCSVInterpreter
        -> FareAttributesTableInterpreter
        -> outputPort;

    block FareAttributesFilePicker oftype FilePicker {
        path: "/fare_attributes.txt";
    }

    block FareAttributesTextFileInterpreter oftype TextFileInterpreter { }
    block FareAttributesCSVInterpreter oftype CSVInterpreter { }

    block FareAttributesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: FareAttributes;
        parseWith: FareAttributesParser;
    }
}
