// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Frequencies {
  property tripId oftype text;
  property startTime oftype GTFSTime;
  property endTime oftype GTFSTime;
  property headwaySecs oftype GTFSNonNegativeInteger;
}
transform FrequenciesParser {
  from r oftype Collection<text>;
  to frequencies oftype Frequencies;

  frequencies: {
    tripId: r . 0,
    startTime: r . 1,
    endTime: r . 2,
    headwaySecs: r . 3,
  };
}

/**
* A GTFSFrequenciesInterpreter interprets a frequencies.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#frequenciestxt
*/
publish composite blocktype GTFSFrequenciesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> FrequenciesFilePicker
        -> FrequenciesTextFileInterpreter
        -> FrequenciesCSVInterpreter
        -> FrequenciesTableInterpreter
        -> outputPort;

    block FrequenciesFilePicker oftype FilePicker {
        path: "/frequencies.txt";
    }

    block FrequenciesTextFileInterpreter oftype TextFileInterpreter { }
    block FrequenciesCSVInterpreter oftype CSVInterpreter { }

    block FrequenciesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Frequencies;
        parseWith: FrequenciesParser;
    }
}
