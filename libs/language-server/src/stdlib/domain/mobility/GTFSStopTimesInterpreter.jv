// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype StopTime {
  property tripId oftype text;
  property arrivalId oftype GTFSTime;
  property departureTime oftype GTFSTime;
  property stopId oftype text;
  property stopSequence oftype GTFSNonNegativeInteger;
  property stopHeadsign oftype text;
  property pickupType oftype text;
  property dropOffTime oftype text;
  property shapeDistTraveled oftype text;
}
transform StopTimeParser {
  from r oftype SheetRow;
  to stopTime oftype StopTime;

  stopTime: {
    tripId: r . 0,
    arrivalId: r . 1,
    departureTime: r . 2,
    stopId: r . 3,
    stopSequence: r . 4,
    stopHeadsign: r . 5,
    pickupType: r . 6,
    dropOffTime: r . 7,
    shapeDistTraveled: r . 8,
  };
}

/**
* A GTFSStopTimesInterpreter interprets a stop_times.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#stop_timestxt
*/
publish composite blocktype GTFSStopTimesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> StopTimesFilePicker
        -> StopTimesTextFileInterpreter
        -> StopTimesCSVInterpreter
        -> StopTimesTableInterpreter
        -> outputPort;

    block StopTimesFilePicker oftype FilePicker {
        path: "/stop_times.txt";
    }

    block StopTimesTextFileInterpreter oftype TextFileInterpreter { }
    block StopTimesCSVInterpreter oftype CSVInterpreter { }

    block StopTimesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: StopTime;
        parseWith: StopTimeParser;
    }
}
