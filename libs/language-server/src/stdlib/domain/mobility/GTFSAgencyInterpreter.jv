// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Agency {
  property agencyId oftype text; // Conditional columns are considered required for now
  property agencyName oftype text;
  property agencyUrl oftype GTFSUrl;
  property agencyTimezone oftype text;
}
transform AgencyParser {
  from r oftype Collection<text>;
  to agency oftype Agency;

  agency: {
    agencyId: r . "agency_id",
    agencyName: r . "agency_name",
    agencyUrl: r . "agency_url",
    agencyTimezone: r . "agency_timezone",
  };
}

/**
* A GTFSAgencyInterpreter interprets a agency.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#agencytxt
*/
publish composite blocktype GTFSAgencyInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> AgencyFilePicker
        -> AgencyTextFileInterpreter
        -> AgencyCSVInterpreter
        -> AgencyTableInterpreter
        -> outputPort;

    block AgencyFilePicker oftype FilePicker {
        path: "/agency.txt";
    }

    block AgencyTextFileInterpreter oftype TextFileInterpreter { }
    block AgencyCSVInterpreter oftype CSVInterpreter { }

    block AgencyTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Agency;
        parseWith: AgencyParser;
    }
}
