// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Stop {
  property stopId oftype text;
  property stopName oftype text;
  property stopDesc oftype text;
  property stopLat oftype GTFSLatitude;
  property stopLon oftype GTFSLongitude;
  property zoneId oftype text;
  property stopUrl oftype text;
}
transform StopParser {
  from r oftype Collection<text>;
  to stop oftype Stop;

  stop: {
    stopId: r . 0,
    stopName: r . 1,
    stopDesc: r . 2,
    stopLat: r . 3,
    stopLon: r . 4,
    zoneId: r . 5,
    stopUrl: r . 6,
  };
}

/**
* A GTFSStopsInterpreter interprets a stops.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#stopstxt
*/
publish composite blocktype GTFSStopsInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> StopsFilePicker
        -> StopsTextFileInterpreter
        -> StopsCSVInterpreter
        -> StopsTableInterpreter
        -> outputPort;

    block StopsFilePicker oftype FilePicker {
        path: "/stops.txt";
    }

    block StopsTextFileInterpreter oftype TextFileInterpreter { }
    block StopsCSVInterpreter oftype CSVInterpreter { }

    block StopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Stop;
        parseWith: StopParser;
    }
}
