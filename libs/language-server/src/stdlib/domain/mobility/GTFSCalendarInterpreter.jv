// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

/**
* A GTFSCalendarInterpreter interprets a calendar.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#calendartxt
*/
publish composite blocktype GTFSCalendarInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> CalendarFilePicker
        -> CalendarTextFileInterpreter
        -> CalendarCSVInterpreter
        -> CalendarTableInterpreter
        -> outputPort;

    block CalendarFilePicker oftype FilePicker {
        path: "/calendar.txt";
    }

    block CalendarTextFileInterpreter oftype TextFileInterpreter { }
    block CalendarCSVInterpreter oftype CSVInterpreter { }

    block CalendarTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "service_id" oftype text,
            "monday" oftype GTFSEnumTwo, // 1 - Service is available for all Mondays in the date range.
                                         // 0 - Service is not available for Mondays in the date range.
            "tuesday" oftype GTFSEnumTwo,
            "wednesday" oftype GTFSEnumTwo,
            "thursday" oftype GTFSEnumTwo,
            "friday" oftype GTFSEnumTwo,
            "saturday" oftype GTFSEnumTwo,
            "sunday" oftype GTFSEnumTwo,
            "start_date" oftype GTFSDate,
            "end_date" oftype GTFSDate
        ];
    }
}
