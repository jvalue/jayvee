// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Shape {
  property shapeId oftype text;
  property shapePtLat oftype GTFSLatitude;
  property shapePtLon oftype GTFSLongitude;
  property shapePtSequence oftype GTFSNonNegativeInteger;
  property shapeDistTraveled oftype text;
}
transform ShapeParser {
  from r oftype SheetRow;
  to shape oftype Shape;

  shape: {
    shapeId: r . 0,
    shapePtLat: r . 1,
    shapePtLon: r . 2,
    shapePtSequence: r . 3,
    shapeDistTraveled: r . 4,
  };
}

/**
* A GTFSShapesInterpreter interprets a shapes.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#shapestxt
*/
publish composite blocktype GTFSShapesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> ShapesFilePicker
        -> ShapesTextFileInterpreter
        -> ShapesCSVInterpreter
        -> ShapesTableInterpreter
        -> outputPort;

    block ShapesFilePicker oftype FilePicker {
        path: "/shapes.txt";
    }

    block ShapesTextFileInterpreter oftype TextFileInterpreter { }
    block ShapesCSVInterpreter oftype CSVInterpreter { }

    block ShapesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Shape;
        parseWith: ShapeParser;
    }
}
