// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Route {
  property routeId oftype text;
  property agencyId oftype text;
  property routeShortName oftype text;
  property routeLongName oftype text;
  property routeDesc oftype text;
  property routeType oftype integer; // Technically is an enum from 0 - 12
  property routeUrl oftype text;
  property routeColor oftype text;
  property routeTextColor oftype text;
}
transform RouteParser {
  from r oftype Collection<text>;
  to route oftype Route;

  route: {
    routeId: r cellInColumn 0,
    agencyId: r cellInColumn 1,
    routeShortName: r cellInColumn 2,
    routeLongName: r cellInColumn 3,
    routeDesc: r cellInColumn 4,
    routeType: r cellInColumn 5,
    routeUrl: r cellInColumn 6,
    routeColor: r cellInColumn 7,
    routeTextColor: r cellInColumn 8,
  };
}

/**
* A GTFSRoutesInterpreter interprets a routes.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#routestxt
*/
publish composite blocktype GTFSRoutesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> RoutesFilePicker
        -> RoutesTextFileInterpreter
        -> RoutesCSVInterpreter
        -> RoutesTableInterpreter
        -> outputPort;

    block RoutesFilePicker oftype FilePicker {
        path: "/routes.txt";
    }

    block RoutesTextFileInterpreter oftype TextFileInterpreter { }
    block RoutesCSVInterpreter oftype CSVInterpreter { }

    block RoutesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Route;
        parseWith: RouteParser;
    }
}
