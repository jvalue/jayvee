// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype FareRules {
  property fareId oftype text;
  property routeId oftype text;
  property originId oftype text;
  property destinationId oftype text;
  property containsId oftype text;
}
transform FareRulesParser {
  from r oftype SheetRow;
  to fareRules oftype FareRules;

  fareRules: {
    fareId: r . 0,
    routeId: r . 1,
    originId: r . 2,
    destinationId: r . 3,
    containsId: r . 4,
  };
}

/**
* A GTFSFareRulesInterpreter interprets a fare_rules.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#fare_rulestxt
*/
publish composite blocktype GTFSFareRulesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> FareRulesFilePicker
        -> FareRulesTextFileInterpreter
        -> FareRulesCSVInterpreter
        -> FareRulesTableInterpreter
        -> outputPort;

    block FareRulesFilePicker oftype FilePicker {
        path: "/fare_rules.txt";
    }

    block FareRulesTextFileInterpreter oftype TextFileInterpreter { }
    block FareRulesCSVInterpreter oftype CSVInterpreter { }

    block FareRulesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: FareRules;
        parseWith: FareRulesParser;
    }
}
