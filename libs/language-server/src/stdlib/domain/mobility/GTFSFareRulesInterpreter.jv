// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype FareRules {
  property fareId oftype text;
  property routeId oftype text;
  property originId oftype text;
  property destinationId oftype text;
  property containsId oftype text;
}
transform FareRulesParser {
  from r oftype Collection<text>;
  to fareRules oftype FareRules;

  fareAttributes: {
    fareId: r cellInColumn 0,
    routeId: r cellInColumn 1,
    originId: r cellInColumn 2,
    destinationId: r cellInColumn 3,
    containsId: r cellInColumn 4,
  };
}

/**
* A GTFSFareRulesInterpreter interprets a fare_rules.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#fare_rulestxt
*/
publish composite blocktype GTFSFareRulesInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> FareRulesFilePicker
        -> FareRulesTextFileInterpreter
        -> FareRulesCSVInterpreter
        -> FareRulesTableInterpreter
        -> outputPort;

    block FareRulesFilePicker oftype FilePicker {
        path: "/fare_rules.txt";
    }

    block FareRulesTextFileInterpreter oftype TextFileInterpreter { }
    block FareRulesCSVInterpreter oftype CSVInterpreter { }

    block FareRulesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: FareRules;
        parseWith: FareRulesParser;
    }
}
