// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

valuetype Trip {
  property routeId oftype text;
  property serviceId oftype text;
  property tripId oftype text;
  property tripHeadsign oftype text;
  property directionId oftype text;
  property blockId oftype text;
  property shapeId oftype text;
}
transform TripParser {
  from r oftype Collection<text>;
  to trip oftype Trip;

  trip: {
    routeId: r cellInColumn 0,
    serviceId: r cellInColumn 1,
    tripId: r cellInColumn 2,
    tripHeadsign: r cellInColumn 3,
    directionId: r cellInColumn 4,
    blockId: r cellInColumn 5,
    shapeId: r cellInColumn 6,
  };
}

/**
* A GTFSTripsInterpreter interprets a trips.txt file from an extracted ZIP file according to the GTFS standard
* See https://gtfs.org/schedule/reference/#tripstxt
*/
publish composite blocktype GTFSTripsInterpreter {

    input inputPort oftype FileSystem;
    output outputPort oftype Table;

    inputPort
        -> TripsFilePicker
        -> TripsTextFileInterpreter
        -> TripsCSVInterpreter
        -> TripsTableInterpreter
        -> outputPort;

    block TripsFilePicker oftype FilePicker {
        path: "/trips.txt";
    }

    block TripsTextFileInterpreter oftype TextFileInterpreter { }
    block TripsCSVInterpreter oftype CSVInterpreter { }

    block TripsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: Trip;
        parseWith: TripParser;
    }
}
